plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

group 'no.unit.nva'
version '1.0-SNAPSHOT'



wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '7.2'
}

sourceCompatibility = JavaVersion.VERSION_11  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_11 // bytecode target version

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

def junit5Version = '5.7.0'
def zalandoVersion = '0.26.0'
def nvaCommonsVersion = '1.8.0'

dependencies {

    implementation group: 'com.github.bibsysdev', name: 'apigateway', version: nvaCommonsVersion
    implementation group: 'com.github.bibsysdev', name: 'core', version: nvaCommonsVersion

    implementation group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '2.2.7'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    implementation group: 'org.zalando', name: 'problem', version: zalandoVersion
    implementation group: 'org.zalando', name: 'jackson-datatype-problem', version: zalandoVersion

    testImplementation group: 'com.github.bibsysdev', name: 'logutils', version: nvaCommonsVersion
    testImplementation group: 'com.github.bibsysdev', name: 'nva-testutils', version: '0.1.22'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version

}

test {
    useJUnitPlatform()
    failFast = true
    testLogging{
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
    environment "ALLOWED_ORIGIN", "*"
    environment "PERSON_AUTHORITY_BASE_ADDRESS", "https://localhost/person"
    environment "BARE_HOST","www.example.com"
    environment "BARE_API_KEY","someKey"
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco")
    }
}

jacoco {
    toolVersion = "0.8.5"
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.00
            }
        }
    }
}

